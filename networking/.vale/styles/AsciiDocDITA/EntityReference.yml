# Report unsupported character entity references.
---
extends: script
message: "HTML character entity references are not supported in DITA."
level: error
link: https://github.com/jhradilek/asciidoctor-dita-vale/blob/main/README.md#errors
scope: raw
script: |
  text               := import("text")
  matches            := []

  r_attribute_list   := text.re_compile("^\\[(?:|[\\w.#%{,\"'].*)\\][ \\t]*$")
  r_block_title      := text.re_compile("^\\.{1,2}[^ \\t.].*$")
  r_code_block       := text.re_compile("^(?:\\.{4,}|-{4,})[ \\t]*$")
  r_comment_line     := text.re_compile("^(//|//[^/].*)$")
  r_comment_block    := text.re_compile("^/{4,}\\s*$")
  r_empty_line       := text.re_compile("^[ \\t]*$")
  r_entity_reference := text.re_compile("&[a-zA-Z][a-zA-Z0-9]*;")
  r_list_continue    := text.re_compile("^\\+[ \\t]*$")
  r_sub_disabled     := text.re_compile("-replacements")
  r_sub_replacements := text.re_compile("subs=['\\x22](?:[^'\\x22]*,[ \\t]*|)\\+?(?:replacements|normal)(?:[ \\t]*,[^'\\x22]*|)['\\x22]")
  r_supported_entity := text.re_compile("&(?:amp|lt|gt|apos|quot);")

  document           := text.split(text.trim_suffix(scope, "\n"), "\n")

  in_code_block      := false
  in_comment_block   := false
  replacements       := false
  start              := 0
  end                := 0

  for line in document {
    start += end
    end    = len(line) + 1

    if r_comment_block.match(line) {
      delimiter := text.trim_space(line)
      if ! in_comment_block {
        in_comment_block = delimiter
      } else if in_comment_block == delimiter {
        in_comment_block = false
      }
      continue
    }
    if in_comment_block { continue }
    if r_comment_line.match(line) { continue }

    if r_code_block.match(line) {
      delimiter := text.trim_space(line)
      if ! in_code_block {
        in_code_block = delimiter
      } else if in_code_block == delimiter {
        in_code_block = false
        replacements = false
      }
      continue
    }
    if in_code_block && ! replacements { continue }

    if r_block_title.match(line) { continue }
    if r_empty_line.match(line) { continue }
    if r_list_continue.match(line) { continue }

    if r_attribute_list.match(line) {
      if r_sub_replacements.match(line) && ! r_sub_disabled.match(line) {
        replacements = true
      }
      continue
    }

    if replacements && ! in_code_block {
      replacements = false
    }

    for i, entry in r_entity_reference.find(line, -1) {
      if ! r_supported_entity.match(entry[0].text) {
        matches = append(matches, {begin: start + entry[0].begin, end: start + entry[0].end - 1})
      }
    }
  }
