# Report that sidebars are not supported.
---
extends: script
message: "Sidebars are not supported in DITA."
level: warning
link: https://github.com/jhradilek/asciidoctor-dita-vale/blob/main/README.md#warnings
scope: raw
script: |
  text              := import("text")
  matches           := []

  r_code_block      := text.re_compile("^(?:\\.{4,}|-{4,})[ \\t]*$")
  r_comment_block   := text.re_compile("^/{4,}\\s*$")
  r_comment_line    := text.re_compile("^(//|//[^/].*)$")
  r_sidebar_block   := text.re_compile("^\\[sidebar\\b[^\\]]*?\\][ \\t]*$")
  r_sidebar_delim   := text.re_compile("^\\*{4,}[ \\t]*$")

  document          := text.split(text.trim_suffix(scope, "\n"), "\n")

  in_code_block     := false
  in_comment_block  := false
  start             := 0
  end               := 0

  for line in document {
    start += end
    end    = len(line) + 1

    if r_comment_block.match(line) {
      delimiter := text.trim_space(line)
      if ! in_comment_block {
        in_comment_block = delimiter
      } else if in_comment_block == delimiter {
        in_comment_block = false
      }
      continue
    }
    if in_comment_block { continue }
    if r_comment_line.match(line) { continue }

    if r_code_block.match(line) {
      delimiter := text.trim_space(line)
      if ! in_code_block {
        in_code_block = delimiter
      } else if in_code_block == delimiter {
        in_code_block = false
      }
      continue
    }
    if in_code_block { continue }

    if r_sidebar_block.match(line) || r_sidebar_delim.match(line) {
      matches = append(matches, {begin: start, end: start + end - 1})
    }
  }
