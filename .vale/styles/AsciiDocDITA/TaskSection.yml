# Report sections inside of procedure modules.
---
extends: script
message: "Sections are not allowed in DITA tasks."
level: error
scope: raw
script: |
  text              := import("text")
  matches           := []

  r_comment_line    := text.re_compile("^(//|//[^/].*)$")
  r_comment_block   := text.re_compile("^/{4,}\\s*$")
  r_content_type    := text.re_compile("^:_(?:mod-docs-content|content|module)-type:[ \\t]+(?i:procedure)")
  r_section         := text.re_compile("^={2,}[ \\t]\\S.*$")

  document          := text.split(text.trim_suffix(scope, "\n"), "\n")

  in_comment_block  := false
  is_procedure      := false
  start             := 0
  end               := 0

  for line in document {
    start += end
    end    = len(line) + 1

    if r_comment_block.match(line) {
      delimiter := text.trim_space(line)
      if ! in_comment_block {
        in_comment_block = delimiter
      } else if in_comment_block == delimiter {
        in_comment_block = false
      }
      continue
    }
    if in_comment_block { continue }
    if r_comment_line.match(line) { continue }

    if r_content_type.match(line) {
      is_procedure = true
    } else if r_section.match(line) {
      matches = append(matches, {begin: start, end: start + end - 1})
    }
  }

  if ! is_procedure {
    matches = []
  }
