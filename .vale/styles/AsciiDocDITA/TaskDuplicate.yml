# Report duplicate titles inside of procedure modules.
---
extends: script
message: "Duplicate titles cannot be mapped to DITA tasks."
level: warning
scope: raw
script: |
  text              := import("text")
  matches           := []

  r_comment_line    := text.re_compile("^(//|//[^/].*)$")
  r_comment_block   := text.re_compile("^/{4,}\\s*$")
  r_content_type    := text.re_compile("^:_(?:mod-docs-content|content|module)-type:[ \\t]+(?i:procedure)")

  titles            := {}
  titles.prereq      = text.re_compile("^\\.{1,2}Prerequisites?[ \\t]*$")
  titles.steps       = text.re_compile("^\\.{1,2}Procedure[ \\t]*$")
  titles.result      = text.re_compile("^\\.{1,2}(?:Verification|Results?)[ \\t]*$")
  titles.trouble     = text.re_compile("^\\.{1,2}(?:Troubleshooting|Troubleshooting steps?)[ \\t]*$")
  titles.postreq     = text.re_compile("^\\.{1,2}Next steps?[ \\t]*$")
  titles.links       = text.re_compile("^\\.{1,2}Additional resources[ \\t]*$")

  document          := text.split(text.trim_suffix(scope, "\n"), "\n")

  in_comment_block  := false
  is_procedure      := false
  start             := 0
  end               := 0
  block_titles      := {}

  for line in document {
    start += end
    end    = len(line) + 1

    if r_comment_block.match(line) {
      delimiter := text.trim_space(line)
      if ! in_comment_block {
        in_comment_block = delimiter
      } else if in_comment_block == delimiter {
        in_comment_block = false
      }
      continue
    }
    if in_comment_block { continue }
    if r_comment_line.match(line) { continue }

    if r_content_type.match(line) {
      is_procedure = true
    }

    for title, r in titles {
      if ! r.match(line) { continue }

      if block_titles[title] {
        matches = append(matches, {begin: start, end: start + end - 1})
      } else {
        block_titles[title] = true
      }
    }
  }

  if ! is_procedure {
    matches = []
  }
