# Report a missing procedure title in a procedure module.
---
extends: script
message: "The '.Procedure' block title is missing."
level: warning
link: https://github.com/jhradilek/asciidoctor-dita-vale/blob/main/README.md#warnings
scope: raw
script: |
  text              := import("text")
  matches           := [{begin: 0, end: 0}]

  r_code_block      := text.re_compile("^(?:\\.{4,}|-{4,})[ \\t]*$")
  r_comment_block   := text.re_compile("^/{4,}\\s*$")
  r_comment_line    := text.re_compile("^(//|//[^/].*)$")
  r_content_type    := text.re_compile("^:_(?:mod-docs-content|content|module)-type:[ \\t]+(?i:procedure)[ \\t]*$")
  r_procedure_title := text.re_compile("^\\.{1,2}Procedure[ \\t]*$")

  document          := text.split(text.trim_suffix(scope, "\n"), "\n")

  in_code_block     := false
  in_comment_block  := false
  is_procedure      := false

  for line in document {
    if r_comment_block.match(line) {
      delimiter := text.trim_space(line)
      if ! in_comment_block {
        in_comment_block = delimiter
      } else if in_comment_block == delimiter {
        in_comment_block = false
      }
      continue
    }
    if in_comment_block { continue }
    if r_comment_line.match(line) { continue }

    if r_code_block.match(line) {
      delimiter := text.trim_space(line)
      if ! in_code_block {
        in_code_block = delimiter
      } else if in_code_block == delimiter {
        in_code_block = false
      }
      continue
    }
    if in_code_block { continue }

    if r_content_type.match(line) {
      is_procedure = true
    } else if r_procedure_title.match(line) {
      matches = []
      break
    }
  }

  if ! is_procedure {
    matches = []
  }
