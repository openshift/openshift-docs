# Suggest assigning the [role="_abstract"] attribute list to a paragraph to
# be used as short description in DITA.
---
extends: script
message: "Assign [role=\"_abstract\"] to a paragraph to use it as <shortdesc> in DITA."
level: warning
link: https://github.com/jhradilek/asciidoctor-dita-vale/blob/main/README.md#warnings
scope: raw
script: |
  text              := import("text")
  matches           := [{begin: 0, end: 0}]

  r_abstract        := text.re_compile("^\\[role=(?:\"_abstract\"|'_abstract'|_abstract)\\][ \\t]*$")
  r_comment_block   := text.re_compile("^/{4,}\\s*$")
  r_comment_line    := text.re_compile("^(//|//[^/].*)$")
  r_content_type    := text.re_compile("^:_(?:mod-docs-content|content|module)-type:")
  r_document_title  := text.re_compile("^(?:=[ \\t]+[^ \\t].*|ifn?def::\\S*\\[=[ \\t]+[^ \\t].*\\][ \\t]*)$")
  r_excluded_type   := text.re_compile("[ \\t]+(?i:snippet|attributes)[ \\t]*$")

  document          := text.split(text.trim_suffix(scope, "\n"), "\n")

  in_comment_block  := false
  has_title         := false
  start             := 0
  end               := 0

  for line in document {
    start += end
    end    = len(line) + 1

    if r_comment_block.match(line) {
      delimiter := text.trim_space(line)
      if ! in_comment_block {
        in_comment_block = delimiter
      } else if in_comment_block == delimiter {
        in_comment_block = false
      }
      continue
    }
    if in_comment_block { continue }
    if r_comment_line.match(line) { continue }

    if r_document_title.match(line) {
      if ! has_title {
        matches = [{begin: start, end: start + end - 1}]
        has_title = true
      }
      continue
    }

    if (r_content_type.match(line) && r_excluded_type.match(line)) ||
       r_abstract.match(line) {
      matches = []
      break
    }
  }
