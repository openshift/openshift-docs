# Report accidental author lines.
---
extends: script
message: "Author lines are not supported for topics."
level: warning
scope: raw
script: |
  text              := import("text")
  matches           := []

  r_comment_line    := text.re_compile("^(//|//[^/].*)$")
  r_comment_block   := text.re_compile("^/{4,}\\s*$")
  r_document_title  := text.re_compile("^=[ \\t]+\\S.*$")
  r_attribute       := text.re_compile("^:!?\\S[^:]*:")

  document          := text.split(text.trim_suffix(scope, "\n"), "\n")

  in_comment_block  := false
  need_empty_line   := false
  start             := 0
  end               := 0

  for line in document {
    start += end
    end    = len(line) + 1

    if r_comment_block.match(line) {
      delimiter := text.trim_space(line)
      if ! in_comment_block {
        in_comment_block = delimiter
      } else if in_comment_block == delimiter {
        in_comment_block = false
      }
      continue
    }
    if in_comment_block { continue }
    if r_comment_line.match(line) { continue }

    if r_document_title.match(line) {
      if need_empty_line {
        matches = append(matches, {begin: start, end: start + end - 1})
        break
      }
      need_empty_line = true
    } else if need_empty_line {
      if r_attribute.match(line) {
        continue
      } else if text.trim_space(line) != "" {
        matches = append(matches, {begin: start, end: start + end - 1})
      }
      break
    }
  }
