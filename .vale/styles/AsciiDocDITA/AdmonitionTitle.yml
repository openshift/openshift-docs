# Report that admonition titles are not supported.
---
extends: script
message: "Admonition titles are not supported in DITA."
level: warning
scope: raw
script: |
  text               := import("text")
  matches            := []

  r_comment_line     := text.re_compile("^(//|//[^/].*)$")
  r_comment_block    := text.re_compile("^/{4,}\\s*$")
  r_block_title      := text.re_compile("^\\.{1,2}[^ \\t.].*$")
  r_admonition_para  := text.re_compile("^(?:NOTE|TIP|IMPORTANT|WARNING|CAUTION):[ \\t]")
  r_admonition_block := text.re_compile("^\\[(?:NOTE|TIP|IMPORTANT|WARNING|CAUTION)\\][ \\t]*$")
  r_empty_line       := text.re_compile("^[ \\t]*$")

  document           := text.split(text.trim_suffix(scope, "\n"), "\n")

  in_comment_block   := false
  expect_title       := false
  expect_admonition  := false
  start              := 0
  end                := 0

  for line in document {
    start += end
    end    = len(line) + 1

    if r_comment_block.match(line) {
      delimiter := text.trim_space(line)
      if ! in_comment_block {
        in_comment_block = delimiter
      } else if in_comment_block == delimiter {
        in_comment_block = false
      }
      continue
    }
    if in_comment_block { continue }
    if r_comment_line.match(line) { continue }
    if r_empty_line.match(line) { continue }

    if r_block_title.match(line) {
      if expect_title {
        matches = append(matches, {begin: start, end: start + end - 1})
        expect_title = false
        expect_admonition = false
      } else {
        expect_admonition = {begin: start, end: start + end - 1}
      }
    } else if r_admonition_block.match(line) {
      if expect_admonition {
        matches = append(matches, expect_admonition)
        expect_admonition = false
        expect_title = false
      } else {
        expect_title = true
      }
    } else if r_admonition_para.match(line) {
      if expect_admonition {
        matches = append(matches, expect_admonition)
        expect_admonition = false
      }
      expect_title = false
    } else {
      expect_title = false
      expect_admonition = false
    }
  }
