# Report a missing document id.
---
extends: script
message: "The document id assigned to the level 0 heading is missing."
level: warning
link: https://github.com/jhradilek/asciidoctor-dita-vale/blob/main/README.md#warnings
scope: raw
script: |
  text              := import("text")
  matches           := []

  r_attribute_list  := text.re_compile("^\\[(?:|[\\w.#%{,\"'].*)\\][ \\t]*$")
  r_attribute       := text.re_compile("^:!?\\S[^:]*:")
  r_code_block      := text.re_compile("^(?:\\.{4,}|-{4,})[ \\t]*$")
  r_comment_block   := text.re_compile("^/{4,}\\s*$")
  r_comment_line    := text.re_compile("^(//|//[^/].*)$")
  r_conditional     := text.re_compile("^(?:ifn?def|ifeval|endif)::\\S*\\[.*\\][ \\t]*$")
  r_document_title  := text.re_compile("^(?:=[ \\t]+[^ \\t].*|ifn?def::\\S*\\[=[ \\t]+[^ \\t].*\\][ \\t]*)$")
  r_empty_line      := text.re_compile("^[ \\t]*$")
  r_id_attribute    := text.re_compile("^\\[(?:id=['\\x22]?|#|\\[)[A-Za-z_:{}][A-Za-z0-9_.:{}-]*(?:['\\x22]?,?[^\\]]*|,[^\\]]*\\]?|\\]?)\\][ \\t]*$")

  document          := text.split(text.trim_suffix(scope, "\n"), "\n")

  in_code_block     := false
  in_comment_block  := false
  assigned_id       := false
  start             := 0
  end               := 0

  for line in document {
    start += end
    end    = len(line) + 1

    if r_comment_block.match(line) {
      delimiter := text.trim_space(line)
      if ! in_comment_block {
        in_comment_block = delimiter
      } else if in_comment_block == delimiter {
        in_comment_block = false
      }
      continue
    }
    if in_comment_block { continue }
    if r_comment_line.match(line) { continue }

    if r_code_block.match(line) {
      delimiter := text.trim_space(line)
      if ! in_code_block {
        in_code_block = delimiter
      } else if in_code_block == delimiter {
        in_code_block = false
      }
      continue
    }
    if in_code_block { continue }

    if r_id_attribute.match(line) {
      assigned_id = true
      continue
    }

    if r_attribute_list.match(line) { continue }
    if r_attribute.match(line) { continue }
    if r_empty_line.match(line) { continue }
    if r_conditional.match(line) && ! r_document_title.match(line) { continue }

    if r_document_title.match(line) {
      if ! assigned_id {
        matches = append(matches, {begin: start, end: start + end - 1})
      }
      break
    }

    assigned_id = false
  }
