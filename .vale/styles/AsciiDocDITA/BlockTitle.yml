# Report unsupported block titles.
---
extends: script
message: "Block titles can only be assigned to examples, figures, and tables in DITA."
level: warning
scope: raw
script: |
  text              := import("text")
  matches           := []

  r_add_resources   := text.re_compile("^\\.{1,2}Additional resources[ \\t]*$")
  r_attribute_list  := text.re_compile("^\\[(?:|[\\w.#%{,\"'].*)\\][ \\t]*$")
  r_attribute       := text.re_compile("^:!?\\S[^:]*:")
  r_block_title     := text.re_compile("^\\.{1,2}[^ \\t.].*$")
  r_comment_block   := text.re_compile("^/{4,}\\s*$")
  r_comment_line    := text.re_compile("^(//|//[^/].*)$")
  r_conditional     := text.re_compile("^(?:ifn?def|ifeval|endif)::\\S*\\[.*\\][ \\t]*$")
  r_content_type    := text.re_compile("^:_(?:mod-docs-content|content|module)-type:[ \\t]+(?i:procedure)")
  r_empty_line      := text.re_compile("^[ \\t]*$")
  r_example_block   := text.re_compile("^\\[example\\][ \\t]*$")
  r_example_delim   := text.re_compile("^={4,}[ \\t]*$")
  r_image           := text.re_compile("^image::(?:\\S|\\S.*\\S)\\[.*\\][ \\t]*$")
  r_supported_title := text.re_compile("^\\.{1,2}(?:Prerequisites?|Procedure|Verification|Results?|Troubleshooting|Troubleshooting steps?|Next steps?)[ \\t]*$")
  r_table_cell      := text.re_compile("^\\.[^ \\t|]+\\|")
  r_table           := text.re_compile("^\\|={3,}[ \\t]*$")

  document          := text.split(text.trim_suffix(scope, "\n"), "\n")

  in_comment_block  := false
  is_procedure      := false
  expect_block      := false
  start             := 0
  end               := 0

  for line in document {
    start += end
    end    = len(line) + 1

    if r_comment_block.match(line) {
      delimiter := text.trim_space(line)
      if ! in_comment_block {
        in_comment_block = delimiter
      } else if in_comment_block == delimiter {
        in_comment_block = false
      }
      continue
    }
    if in_comment_block { continue }
    if r_comment_line.match(line) { continue }

    if r_content_type.match(line) {
      is_procedure = true
      continue
    }

    if r_attribute_list.match(line) && ! r_example_block.match(line) { continue }
    if r_attribute.match(line) { continue }
    if r_conditional.match(line) { continue }
    if r_empty_line.match(line) { continue }

    if r_block_title.match(line) {
      if is_procedure && r_supported_title.match(line) { continue }
      if r_add_resources.match(line) { continue }
      if r_table_cell.match(line) { continue }

      expect_block = {begin: start, end: start + end -1}
      continue
    }

    if r_table.match(line) || r_image.match(line) ||
       r_example_block.match(line) || r_example_delim.match(line) {
      expect_block = false
      continue
    }

    if expect_block {
      matches = append(matches, expect_block)
    }

    expect_block = false
  }
